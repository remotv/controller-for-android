apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName]
    } else {
        return "";
    }
}

android {
    compileSdkVersion 28

    signingConfigs {
        release {
            storeFile file("../deploy.jks")
            storePassword System.getenv("KEY_PASS_RELEASE")
            keyAlias System.getenv("KEY_ALIAS_RELEASE")
            keyPassword System.getenv("KEY_PASS_RELEASE")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "tv.remo.android.controller"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 15
        versionName "0.17.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'

    buildTypes {
        debug{
            def testKey = System.getenv("TEST_KEY")
            if(testKey == null)
                testKey = getProps('TEST_KEY')
            if(testKey == null)
                testKey = ""

            buildConfigField "String", "robot_test_key", "\"$testKey\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            if(System.getenv("KEY_ALIAS_RELEASE")){
                signingConfig signingConfigs.release
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation project(':sdk')
    implementation project(':licensehelper')
    implementation project(':settingsutil')

    //Test sdks
    testImplementation 'junit:junit:4.12'

    // Core library
    androidTestImplementation 'androidx.test:core:1.3.0-alpha05'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha05'
    androidTestImplementation 'androidx.test:rules:1.3.0-alpha05'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha05'
    def mockito_version = "2.23.4"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
}
